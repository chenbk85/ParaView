#==========================================================================
#
#     Program: ParaView
#
#     Copyright (c) 2005-2008 Sandia Corporation, Kitware Inc.
#     All rights reserved.
#
#     ParaView is a free software; you can redistribute it and/or modify it
#     under the terms of the ParaView license version 1.2.
#
#     See License_v1.2.txt for the full ParaView license.
#     A copy of this license can be obtained by contacting
#     Kitware Inc.
#     28 Corporate Drive
#     Clifton Park, NY 12065
#     USA
#
#  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
#  ``AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
#  LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
#  A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE AUTHORS OR
#  CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
#  EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
#  PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
#  PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
#  LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
#  NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
#  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
#==========================================================================
if (vtk-module)
  # We don't want this module to be processed as a part of the module-dependency
  # processing. Hence we skip it.
  return()
endif()

set (vtk-module vtkPVPythonCatalyst)
set (${vtk-module}_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
set (${vtk-module}_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR})

set (Module_SRCS
  cp_helper_py.cxx
  CPythonAdaptorAPI.cxx
  vtkCPPythonAdaptorAPI.cxx
  vtkCPPythonHelper.cxx
  vtkCPPythonProcessor.cxx
  vtkCPPythonScriptPipeline.cxx
)

# Embed helper Python script inside code base
add_custom_command(
    OUTPUT  "${CMAKE_CURRENT_BINARY_DIR}/cp_helper_py.cxx"
    DEPENDS "${CMAKE_CURRENT_SOURCE_DIR}/cp_helper.py"
            vtkEncodeString
    COMMAND vtkEncodeString
    ARGS "${CMAKE_CURRENT_BINARY_DIR}/cp_helper_py.cxx"
         "${CMAKE_CURRENT_SOURCE_DIR}/cp_helper.py"
         cp_helper_py)

set_source_files_properties(cp_helper_py WRAP_EXCLUDE)

configure_file(vtkCPPythonHelperConfig.h.in
               vtkCPPythonHelperConfig.h @ONLY)


set (${vtk-module}_HDRS CPythonAdaptorAPI.h)

set (CATALYST_FORTRAN_USING_MANGLING)
if (CMAKE_Fortran_COMPILER_WORKS)
  include(FortranCInterface)
  FortranCInterface_HEADER(FortranPythonAdaptorAPIMangling.h
    SYMBOLS coprocessorinitializewithpython)
  set(CATALYST_FORTRAN_USING_MANGLING ${FortranCInterface_GLOBAL_FOUND})
  set (Module_SRCS
      ${Module_SRCS}
      FortranPythonAdaptorAPI.cxx)

  set (${vtk-module}_HDRS
       ${${vtk-module}_HDRS}
       FortranPythonAdaptorAPI.h
       FortranPythonAdaptorAPIMangling.h)
endif()

vtk_module_library(${vtk-module} ${Module_SRCS})
target_link_libraries(${vtk-module} vtkUtilitiesPythonInitializer)

if (CATALYST_FORTRAN_USING_MANGLING)
  set_property(TARGET ${vtk-module} APPEND PROPERTY COMPILE_DEFINITIONS
      "CATALYST_FORTRAN_USING_MANGLING=1")
endif()
